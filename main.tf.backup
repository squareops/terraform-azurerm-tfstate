locals {
  region           = "East US"
  environment      = "demo"
  name             = "skaf"
  type             = "tfbackend"
}

resource "azurerm_resource_group" "terraform_backend" {
  name     = format("%s-%s-%s-rg", local.name, local.environment, local.type)
  location = local.region

  tags = {
    "Name" = format("%s-%s-%s-rg", local.name, local.environment, local.type)
  }
}

resource "azurerm_storage_account" "terraform_backend" {
  depends_on = [azurerm_resource_group.terraform_backend]

  name                     = format("%s%s%s", local.name, local.environment, local.type)
  resource_group_name      = azurerm_resource_group.terraform_backend.name
  location                 = azurerm_resource_group.terraform_backend.location
  account_kind             = "StorageV2"
  account_tier             = "Standard"
  access_tier              = "Hot"
  account_replication_type = "LRS"

  tags = {
    "Name" = format("%s-%s-%s-sa", local.name, local.environment, local.type)
  }
}

resource "azurerm_storage_container" "terraform_backend" {
  depends_on = [azurerm_storage_account.terraform_backend]
  name                 = format("%s-%s-%s-sc", local.name, local.environment, local.type)
  storage_account_name = azurerm_storage_account.terraform_backend.name
}

# --- For KMS encryption keys ---

data "azuread_client_config" "current" {}

resource "azurerm_key_vault" "kms" {
  name                        = format("%s-%s-kms", local.name, local.environment)
  location                    = local.region
  resource_group_name         = azurerm_resource_group.terraform_backend.name
  enabled_for_disk_encryption = true
  tenant_id                   = data.azuread_client_config.current.tenant_id
  soft_delete_retention_days  = 7
  purge_protection_enabled    = false

  sku_name = "standard"

  access_policy {
    tenant_id = data.azuread_client_config.current.tenant_id
    object_id = data.azuread_client_config.current.object_id

    key_permissions = [
      "Backup",
      "Delete",
      "Get",
      "List",
      "Purge",
      "Recover",
      "Restore"
    ]

    secret_permissions = [
      "Backup",
      "Delete",
      "Get",
      "List",
      "Purge",
      "Recover",
      "Restore"
    ]

    storage_permissions = [
      "Backup",
      "Delete",
      "Get",
      "List",
      "Purge",
      "Recover",
      "Restore"
    ]
  }
}